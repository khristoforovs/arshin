units_list = _{ SOI ~ unit_definition+ ~ EOI }
unit_definition = {"unit" ~ identifier ~ "{" ~ dimension_property ~ transformation_property ~ prefixes_property ~ "}"}
identifier = { (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* } 

dimension_property = { "dimension:" ~ dimension_expression }
dimension_expression = { dimension_term ~ ( operator ~ dimension_term )* }
dimension_term = { fundamental ~ ("^" ~ exponent)? }
exponent = @{ ("-"? ~ ASCII_DIGIT+) }
fundamental = @{ "length" | "mass" | "time" | "current" | "temperature" |
    "amount of substance" | "luminous intensity" | "angle" | "bit" | "count" }
operator = @{ "*" | "/" }

transformation_property = { "transformation:" ~ transformation }
transformation = { identity_transformation | linear_transformation | decibel_transformation }
identity_transformation = { "identity" }
linear_transformation = { "linear(" ~ "scale:" ~ number ~ ("," ~ "offset:" ~ number)? ~ ")" }
decibel_transformation = { "decibel(" ~ "p0:" ~ number ~ ")" }
number = @{ "-"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT*)? ~ ("e" ~ "-"? ~ ASCII_DIGIT+)? }

prefixes_property = { "prefixes:" ~ prefixes_expression }
prefixes_expression = @{ "standard" | "no" }

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* ~ ("\n" | EOI) }
